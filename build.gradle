plugins {
    id("net.minecraftforge.gradle") version("${fg_plugin_version}")
    id("org.spongepowered.mixin") version("${mixin_plugin_version}")
    id("eclipse")
    id("idea")
    id("maven-publish")
    id("org.parchmentmc.librarian.forgegradle") version("${librarian_plugin_version}")
    id("com.github.johnrengelman.shadow") version("${shadow_plugin_version}")
    id("me.modmuss50.mod-publish-plugin") version("${publisher_version}")
}

version = "${mc_version}-${mod_version}"
group = mod_group
archivesBaseName = modid

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: mappings_channel, version: mappings_version
    
    copyIdeResources = true

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            arg "-mixin.config=${modid}.mixins.json"

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            jvmArgs "-Xms4G", "-Xmx8G", "-XX:+AllowEnhancedClassRedefinition", "-XX:HotswapAgent=fatjar", "-XX:+UseG1GC", "-XX:+UnlockExperimentalVMOptions", "-XX:MaxGCPauseMillis=100", "-XX:+DisableExplicitGC", "-XX:TargetSurvivorRatio=90", "-XX:G1NewSizePercent=35", "-XX:G1MaxNewSizePercent=60", "-XX:G1MixedGCLiveThresholdPercent=50", "-XX:+AlwaysPreTouch"
            mods {
                modid {
                    source sourceSets.main
                }
            }
        }

        client {
            args '--username', "ManMeme####"
            property 'forge.enabledGameTestNamespaces', modid
        }
        server {
            property 'forge.enabledGameTestNamespaces', modid
        }
        gameTestServer {
            property 'forge.enabledGameTestNamespaces', modid
        }

        data {
            args '--mod', modid, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
    ext.refMap = "main.refmap.json"
}

mixin {
    add sourceSets.main, "${modid}.refmap.json"
    config "${modid}.mixins.json"
    debug.export = true
}

repositories {
    maven {
        name = "CurseMaven Maven"
        url = "https://www.cursemaven.com"
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
    maven {
        name = "Geckolib Maven"
        url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
    }
    maven {
        name = "KosmX Maven"
        url = "https://maven.kosmx.dev/"
    }

    maven {
        name = "BlameJared Maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven {
		name = "RyanLiptak Maven"
		url = "https://maven.ryanliptak.com/"
    }
    maven {
        name = "100Media Maven"
        url = "https://maven.100media.dev/"
    }
    maven {
        name = "IllusiveC4 Maven"
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        name = "Iron's Maven - Release"
        url = "https://code.redspace.io/releases"
    }

    maven {
        name = "Iron's Maven - Snapshots"
        url = "https://code.redspace.io/snapshots"
    }
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    // Base
    minecraft("net.minecraftforge:forge:${mc_version}-${forge_version}")
    annotationProcessor("org.spongepowered:mixin:0.8.5:processor")

    // Dependencies (Required)
    implementation fg.deobf("software.bernie.geckolib:geckolib-forge-${mc_version}:${geckolib_version}")
    implementation fg.deobf("dev.kosmx.player-anim:player-animation-lib-forge:${player_animator_version}")

    // Dependencies (Shadowed)
    shade fg.deobf("dev._100media.capabilitysyncer:capabilitysyncer:${capabilitysyncer_version}")

    // Dependencies (Integrations)
    implementation fg.deobf('maven.modrinth:better-combat:1.8.4+1.20.1-forge')
    implementation fg.deobf("curse.maven:cloth-config-348521:4973441")

    implementation fg.deobf("curse.maven:simply-swords-659887:5116912")
    implementation fg.deobf("curse.maven:architectury-api-419699:5137938")

    implementation fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}")

    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:5.7.0+1.20.1")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:5.7.0+1.20.1:api")

    compileOnly fg.deobf("io.redspace.ironsspellbooks:irons_spellbooks:1.20.1-3.0.1")
    runtimeOnly fg.deobf("io.redspace.ironsspellbooks:irons_spellbooks:1.20.1-3.0.1")
    runtimeOnly fg.deobf("top.theillusivec4.caelus:caelus-forge:3.1.0+1.20")
    compileOnly fg.deobf("top.theillusivec4.caelus:caelus-forge:3.1.0+1.20")

    // Dependencies (Extras)
    implementation fg.deobf("curse.maven:just-enough-resources-jer-240630:${jer_version}")
    implementation fg.deobf("curse.maven:just-enough-effect-descriptions-jeed-532286:${jeed_version}")

    implementation fg.deobf("curse.maven:config-menus-forge-544048:${config_menus_version}")
    implementation fg.deobf("curse.maven:catalogue-459701:${catalogue_version}")
    implementation fg.deobf("curse.maven:gamemenumodoption-353051:${gmmo_version}")

    implementation fg.deobf("curse.maven:jade-324717:${jade_version}")

    compileOnly fg.deobf("squeek.appleskin:appleskin-forge:${appleskin_version}:api")
    runtimeOnly fg.deobf("squeek.appleskin:appleskin-forge:${appleskin_version}")

    implementation fg.deobf("curse.maven:placebo-283644:5098182")
    implementation fg.deobf("curse.maven:apothic-attributes-898963:5340012")
    implementation fg.deobf("curse.maven:apotheosis-313970:5317303")
    implementation fg.deobf("curse.maven:mmmmmmmmmmmm-225738:5319203")
    implementation fg.deobf("curse.maven:selene-499980:5068161")
    implementation fg.deobf("curse.maven:lendercataclysm-551586:5409421")
    implementation fg.deobf("curse.maven:lionfish-api-1001614:5363774")
    implementation fg.deobf("curse.maven:xenon-564239:5374393")
    //implementation fg.deobf("curse.maven:oculus-581495:5080444")
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            // Base
            modid: modid,
            mod_name: mod_name,
            mod_version: mod_version,
            mod_authors: mod_authors,
            mod_license: mod_license,
            mod_description: mod_description,
            mod_issue_tracker_url: mod_issue_tracker_url,

            mc_version: mc_version,
            mc_version_range: mc_version_range,
            forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,

            // Dependencies (Required)
            geckolib_version_range: geckolib_version_range,
            player_animator_version_range: player_animator_version_range,

            // Dependencies (Shadowed)
            capabilitysyncer_version: capabilitysyncer_version,

            // Dependencies (Optional)
            jei_version_range: jei_version_range,
            jer_version_range: jer_version_range,
            jeed_version_range: jeed_version_range,

            config_menus_version_range: config_menus_version_range,
            catalogue_version_range: catalogue_version_range,
            gmmo_version_range: gmmo_version_range,

            jade_version_range: jade_version_range,
            appleskin_version_range: appleskin_version_range
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('jar', Jar).configure {
    archiveClassifier = 'slim'
    manifest {
        attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : mod_version,
                'Implementation-Title'    : mod_name,
                'Implementation-Version'  : mod_version,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConfigs'		  	  : "${modid}.mixins.json"
        ])
    }
    finalizedBy("reobfJar")
}

shadowJar {
    archiveClassifier = ''
    configurations = [project.configurations.shade]

    relocate("dev._100media.capabilitysyncer", "${group}.relocated.capabilitysyncer")
    finalizedBy("reobfShadowJar")
}

assemble.dependsOn shadowJar

reobf {
    shadowJar {}
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

publishMods {
    file = jar.archiveFile
    changelog = file("CHANGELOG.md").getText()
    version = "${mc_version}-${mod_version}"
    displayName = "${mod_name} v${mc_version}-${mod_version}"
    type = STABLE

    modLoaders.add("forge")

    curseforge {
        projectId = "872712"
        projectSlug = "celestisynth"
        accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")
        announcementTitle = "New Celestisynth CurseForge Release!"

        getMinecraftVersions().add(mc_version)
        requires("geckolib")
        requires("playeranimator")
    }

    modrinth {
        projectId = "2ocGk7mG"
        accessToken = providers.environmentVariable("MODRINTH_API_KEY")
        announcementTitle = "New Celestisynth Modrinth Release!"

        getMinecraftVersions().add(mc_version)
        requires("geckolib")
        requires("playeranimator")
    }
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}